import io.gitlab.arturbosch.detekt.Detekt

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:7.0.4"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.5"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.19.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'com.stepango.androidjar' version "0.1"
    id "io.gitlab.arturbosch.detekt" version "1.19.0"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.register("detektAll", Detekt) {
    description = "DETEKT build for all modules"
    setSource(projectDir)
    config.setFrom(files("$rootDir/scripts/detekt/detekt.yml"))
    parallel = true
    buildUponDefaultConfig = true
    ignoreFailures = true
    reports {
        xml {
            destination = file("build/reports/detekt/detekt-report.xml")
        }
        html {
            destination = file("build/reports/detekt/detekt-report.html")
        }
        txt.enabled = false
    }
    exclude 'build/'
}

ext {
    targetSdkVersion = 31
    androidJar = androidjar.find(targetSdkVersion)
    dagger = '2.40.1'
    navigation = '2.3.5'
    coroutines = '1.5.1'
    lifecycle = '2.4.0'
    glide = '4.11.0'
    retrofit = '2.9.0'
    loggingInterceptor = '4.2.1'
    shimmer = '0.5.0'
    dataStore = '1.0.0'
    preference = '1.2.0'
    work = '2.7.1'
    coroutinesPlayServices = '1.4.1'
    junit = '1.1.3'
    kaspresso = '1.2.1'
    mockk = '1.12.3'
    lint = '30.0.4'
    detekt = '1.19.0'
}